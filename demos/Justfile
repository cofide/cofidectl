set export
set shell := ["bash", "-euo", "pipefail", "-c"]

namespace := "demo"

# Set prompt_namespace=no to avoid prompting before namespace creation.
prompt_namespace := 'yes'

# Check for demo script dependencies
check-deps:
    for cmd in kubectl; do \
        if ! command -v $cmd &> /dev/null; then \
            echo "Error: $cmd is not installed" >&2; \
            exit 1; \
        fi \
    done
    echo "All dependencies installed"

ensure-namespace context:
    if [[ ! -z "{{context}}" ]]; then \
        if ! kubectl --context {{context}} get namespace "{{namespace}}" &> /dev/null; then \
            echo "Namespace {{namespace}} does not exist"; \
            if [[ "{{prompt_namespace}}" != "no" ]]; then \
                read -p "Create namespace? (y/n) " -r; \
                if [[ ! $REPLY =~ ^[Yy]$ ]]; then \
                    echo "Aborting..."; \
                    exit 1; \
                fi \
            fi; \
            kubectl --context {{context}} create namespace "{{namespace}}"; \
        fi \
    fi

# Deploy ping-pong server and client in Kubernetes
deploy-ping-pong client_context client_spiffe_id server_context="": (ensure-namespace client_context) (ensure-namespace server_context)
    ping-pong/deploy.sh {{namespace}} {{client_context}} {{client_spiffe_id}} {{server_context}}

# Deploy ping-pong server and client in Docker containers
deploy-ping-pong-docker client_name client_spiffe_id server_name workload_api_path:
    ping-pong-docker/deploy.sh {{client_name}} {{client_spiffe_id}} {{server_name}} {{workload_api_path}}
